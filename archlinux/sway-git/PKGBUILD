# Maintainer iFlygo <iflygo at outlook dot com>
# Maintainer: q234 rty <q23456yuiop at gmail dot com>
# Contributor: Brett Cornwall <ainola@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=sway-git
_pkgname=sway
pkgver=r14683.c90cb3dd8f49
pkgrel=1
pkgdesc='Tiling Wayland compositor and replacement for the i3 window manager'
arch=(x86_64)
url='https://swaywm.org/'
license=(MIT)
depends=(
  cairo
  gdk-pixbuf2
  glslang
  json-c
  libdisplay-info
  libinput
  libliftoff
  libxcb
  lcms2
  libxkbcommon
  opengl-driver
  pango
  pcre2
  pixman
  polkit
  seatd
  ttf-font
  vulkan-icd-loader
  wayland
  xcb-util-errors
  xcb-util-renderutil
  xcb-util-wm
  xorg-xwayland
)
makedepends=(git meson scdoc wayland-protocols xorgproto vulkan-headers)
backup=(etc/sway/config)
optdepends=(
  'dmenu: Application launcher'
  'grim: Screenshot utility'
  'mako: Lightweight notification daemon'
  'slurp: Select a region'
  'swayidle: Idle management daemon'
  'swaylock: Screen locker'
  'wallutils: Timed wallpapers'
  'waybar: Highly customizable bar'
  'xorg-xwayland: X11 support'
)
source=("sway::git+https://github.com/swaywm/sway.git"
  "git+https://gitlab.freedesktop.org/wlroots/wlroots.git"
  "50-systemd-user.conf"
  "sway-portals.conf")
provides=("sway")
conflicts=("sway")
install=sway.install
sha512sums=('SKIP'
  'SKIP'
  'b7ec3bf297c72d8e0b38502f41baa73ffff8e9fae7e3b4ad874c7ac476283eb3a84060df496f436ce5a782a967ae7bc6e6dce65ea01b02e0ccd9f5b5b5b8f371'
  '92b665098b63903b2fc60be3769f7b7eceb06d4621cf2bac08d652ca4c912fe2d8129e8d28b68052081682581c112c0b0c29d28828650e3f53b2f4996eb85135'
)

pkgver() {
  cd "$_pkgname"
  _sway_rev=$(git rev-list --count HEAD)
  _sway_short=$(git rev-parse --short=6 HEAD)
  cd ../wlroots
  _wlroots_rev=$(git rev-list --count HEAD)
  _wlroots_short=$(git rev-parse --short=6 HEAD)
  cd "../$_pkgname"
  printf "r%s.%s%s" "$((_sway_rev + $_wlroots_rev))" "$_sway_short" "$_wlroots_short"
}

prepare() {
  cd "$_pkgname"

  # Set the version information to 'Arch Linux' instead of 'makepkg'
  sed -i "s/branch \\\'@1@\\\'/Arch Linux/g" meson.build
}

build() {
  mkdir -p sway/subprojects
  ln -sf ../../wlroots sway/subprojects/wlroots

  arch-meson \
    --buildtype release \
    -D default-wallpaper=false \
    -D sd-bus-provider=libsystemd \
    -D default_library=static \
    -D swaybar=false \
    -D swaynag=false \
    -D werror=false \
    sway build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  # Remove wlroots headers, static library, and pkgconfig file.
  rm -rf "$pkgdir/usr/include" "$pkgdir/usr/lib"

  install -Dm644 "$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -Dm644 50-systemd-user.conf -t "$pkgdir/etc/sway/config.d/"
  install -Dm644 sway-portals.conf "$pkgdir/usr/share/xdg-desktop-portal/sway-portals.conf"
}

# vim: ts=2 sw=2 et
