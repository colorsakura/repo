# Maintainer iFlygo <iflygo at outlook dot com>
# Maintainer: q234 rty <q23456yuiop at gmail dot com>
# Contributor: Brett Cornwall <ainola@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=sway-im-git
_pkgname=sway
pkgver=r7209.a946b1aecfa3.41494244dfa0
pkgrel=1
pkgdesc='Tiling Wayland compositor and replacement for the i3 window manager (git version with input method popups)'
arch=(x86_64)
url='https://swaywm.org/'
license=(MIT)
depends=(
	cairo
	gdk-pixbuf2
	glslang
	json-c
	libdisplay-info
	libinput
	libxcb
	libxkbcommon
	opengl-driver
	pango
	pcre2
	pixman
	polkit
	seatd
	ttf-font
	vulkan-icd-loader
	wayland
	xcb-util-errors
	xcb-util-renderutil
	xcb-util-wm
	xorg-xwayland
)
makedepends=(git meson scdoc wayland-protocols xorgproto vulkan-headers)
backup=(etc/sway/config)
optdepends=(
	'dmenu: Application launcher'
	'grim: Screenshot utility'
	'mako: Lightweight notification daemon'
	'slurp: Select a region'
	'swayidle: Idle management daemon'
	'swaylock: Screen locker'
	'wallutils: Timed wallpapers'
	'waybar: Highly customizable bar'
	'xorg-xwayland: X11 support'
)
source=("sway::git+https://github.com/swaywm/sway.git"
	"git+https://gitlab.freedesktop.org/wlroots/wlroots.git"
	"50-systemd-user.conf"
	"sway-portals.conf"
	"7226.patch"
	"0002-Fix-configure_notify-event.patch")
provides=("sway")
conflicts=("sway")
install=sway.install
sha512sums=('SKIP'
	'SKIP'
	'b7ec3bf297c72d8e0b38502f41baa73ffff8e9fae7e3b4ad874c7ac476283eb3a84060df496f436ce5a782a967ae7bc6e6dce65ea01b02e0ccd9f5b5b5b8f371'
	'ee361b0d091f574a6e5904256774947856779b5cddc87520e6c680f45a8c2b1896ae337dbd71a45eaad1cfc4224199369bff4f2847f70676898f9314d14d020b'
	'affc2bb0060ed060259114385f7f24d27c716eb8fc0d1f027fa96bbe1c468115683055931422b183e6fbba5f3ed77a13edb0efca8f95c4aff4cf1a9f2e265adb'
	'484c8a6848b1409a16403bf0c3e366bf5203716824c24e16efcdcdb71ff34c42b5da6e3da338fafea70ad80fcdd3d6e9f070fb809093d8b0b08ee0b2c95a4de4')

pkgver() {
	cd "$_pkgname"
	printf "r%s.%s%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=6 HEAD)" "$(
		cd ../wlroots &&
			git rev-parse --short=6 HEAD
	)"
}

prepare() {
	cd "$_pkgname"

	# Set the version information to 'Arch Linux' instead of 'makepkg'
	sed -i "s/branch \\\'@1@\\\'/Arch Linux/g" meson.build

	patch -Np1 -i ../7226.patch

	cd "../wlroots"
	git revert -n 18595000f3a21502fd60bf213122859cc348f9af
	patch -Np1 <../0002-Fix-configure_notify-event.patch
}

build() {
	mkdir -p sway/subprojects
	ln -sf ../../wlroots sway/subprojects/wlroots

	arch-meson \
		--buildtype release \
		-D sd-bus-provider=libsystemd \
		-D default_library=static \
		-D werror=false \
		sway build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
	# Remove wlroots headers, static library, and pkgconfig file.
	rm -rf "$pkgdir/usr/include" "$pkgdir/usr/lib"

	install -Dm644 "$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
	install -Dm644 50-systemd-user.conf -t "$pkgdir/etc/sway/config.d/"
	install -Dm644 sway-portals.conf "$pkgdir/usr/share/xdg-desktop-portal/sway-portals.conf"
}

# vim: ts=2 sw=2 et
